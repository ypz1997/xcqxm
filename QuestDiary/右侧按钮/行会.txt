[@自定行会]
{
#IF
TRUE
#ACT
MOV N$行会_页签ID 0
MOV N$行会_循环计次 0
MOV S$行会_行会列表子项 
MOV S$行会_Android面板数据  
MOV S$行会_Android二级页面
MOV N$行会_VIP等级限制 $CustomValue(90)
#IF
equal <$CLIENTFLAG> 1
#ACT
GOTO @PC行会面板

#IF
equal <$CLIENTFLAG> 2 
#ACT
GOTO @行会_Android行会列表页签

[@行会_Android面板]
#IF
TRUE
#ACT
split <$STR(S$行会_页签按钮)> | S$行会_页签按钮接受
#SAY
<Button|x=854.0|y=87.0|nimg=public/button/btn_close_01.png|_localZOrder=0|pimg=public/button/btn_close_01.png|link=@exit>
<Layout|x=545|y=0|width=80|height=80|_localZOrder=-1|link=@exit>
<Img|x=268.0|y=92.0|children={hhlb1,hhlb2,hhlb3,hhlb4,hhlb5,hhlb6,hhlb7,hhlb8,hhlb9,hhlb10,hhlb11,hhlb12,hhlb13,hhlb14,hhlb15}|show=0|loadDelay=0|esc=1|bg=1|move=0|img=private/hanghui/hh_bj{$STR(N$行会_页签ID)}.png|_localZOrder=-2|loadDelay=1|reset=1>
<Img|id=hhlb1|x=25.0|y=45.0|img=public/tab_bg_{$STR(S$行会_页签按钮接受_0)}.png|esc=0|link=@行会_Android行会列表页签>
<Img|id=hhlb2|x=25.0|y=45.0|img=private/hanghui/hh_hanghuiliebiao.png|esc=0>
<Img|id=hhlb3|x=131.0|y=45.0|img=public/tab_bg_{$STR(S$行会_页签按钮接受_1)}.png|esc=0|link=@行会_Android行会招募页签>
<Img|id=hhlb4|x=131.0|y=45.0|img=private/hanghui/hh_hanghuizhaomu.png|esc=0>
<Img|id=hhlb5|x=237.0|y=45.0|img=public/tab_bg_{$STR(S$行会_页签按钮接受_2)}.png|esc=0|link=@行会_Android行会玩法页签>
<Img|id=hhlb6|x=237.0|y=45.0|img=private/hanghui/hh_hanghuiwanfa.png|esc=0>
<Img|id=hhlb7|x=343.0|y=45.0|img=public/tab_bg_{$STR(S$行会_页签按钮接受_3)}.png|esc=0|link=@行会_Android我的行会页签>
<Img|id=hhlb8|x=343.0|y=45.0|img=private/hanghui/hh_wodehanghui.png|esc=0>
{$STR(S$行会_Android面板数据)}
{$STR(S$行会_Android二级页面)}
{$STR(S$行会_Android仓库页面)}
[@行会_Android行会列表页签]
#IF
!EQUAL N$行会_页签ID 0
#ACT
MOV N$行会_页签ID 0
MOV N$行会_循环计次 0
MOV S$行会_Android面板数据  
MOV S$行会_行会列表子项
#IF
TRUE
#ACT
ReadRandomStr ..\QuestDiary\读取数据\行会信息.txt S$行会_行会信息 <$STR(N$行会_循环计次)>
#IF
POS <$STR(S$行会_行会信息)> 是否开启自动收人
#ACT
split <$STR(S$行会_行会信息)> | S$行会_行会信息拆分

#IF
PosInlist <$STR(S$行会_行会信息拆分_1)>|<$USERNAME> ..\QuestDiary\读取数据\行会申请列表.txt
#ACT
MOV N$行会_申请行会按钮置灰 1
#ELSEACT
MOV N$行会_申请行会按钮置灰 0

#IF
POS <$STR(S$行会_行会信息)> 是否开启自动收人
#ACT
INC S$行会_Android面板数据 <Img|id=hhm{$STR(N$行会_循环计次)}|children={hhm<$STR(N$行会_循环计次)>_0,hhm<$STR(N$行会_循环计次)>_1,hhm<$STR(N$行会_循环计次)>_2,hhm<$STR(N$行会_循环计次)>_3,hhm<$STR(N$行会_循环计次)>_4,hhm<$STR(N$行会_循环计次)>_5,hhm<$STR(N$行会_循环计次)>_6}|x=289.0|y=209.0|img=private/hanghui/hh_liebiaobj.png|esc=0>
INC S$行会_Android面板数据 <Button|id=hhm<$STR(N$行会_循环计次)>_0|x=0.0|y=0|width=160|height=36|pimg=|nimg=|color=255|mimg=|size=18|text={$STR(S$行会_行会信息拆分_1)}>
INC S$行会_Android面板数据 <Button|id=hhm<$STR(N$行会_循环计次)>_1|x=160.0|y=0|width=90|height=36|pimg=|nimg=|color=255|mimg=|size=18|text={$GUILDINFO(S$行会_行会信息拆分_1).MEMBERCOUNT}>
INC S$行会_Android面板数据 <Button|id=hhm<$STR(N$行会_循环计次)>_2|x=248.0|y=0|width=160|height=36|pimg=|nimg=|color=255|mimg=|size=18|text={$STR(S$行会_行会信息拆分_0)}>
INC S$行会_Android面板数据 <Img|id=hhm<$STR(N$行会_循环计次)>_3|x=415.0|y=2.0|grey={$STR(N$行会_申请行会按钮置灰)}|img=public/btn_bg_4.png|esc=0|link=@行会_申请加入行会&行会_申请行会名={$STR(S$行会_行会信息拆分_1)}>
INC S$行会_Android面板数据 <Img|id=hhm<$STR(N$行会_循环计次)>_4|x=510.0|y=2.0|img=public/btn_bg_4.png|esc=0|link=@行会_行会宣战&行会_宣战行会名={$STR(S$行会_行会信息拆分_1)}>
INC S$行会_Android面板数据 <Img|id=hhm<$STR(N$行会_循环计次)>_5|x=415.0|y=2.0|img=private/hanghui/hh_shenqing.png|esc=0>
INC S$行会_Android面板数据 <Img|id=hhm<$STR(N$行会_循环计次)>_6|x=510.0|y=2.0|img=private/hanghui/hh_xuanzhan.png|esc=0>
INC S$行会_行会列表子项 hhm<$STR(N$行会_循环计次)>,


#IF
SMALL <$STR(N$行会_循环计次)> <$TXTLINE(..\QuestDiary\读取数据\行会信息.txt)>
#ACT
INC N$行会_循环计次 1
GOTO @行会_Android行会列表页签

#IF
TRUE
#ACT
MOV S$行会_页签按钮 1|2|2|2
INC S$行会_Android面板数据 <Img|id=hhlb12|x=510.0|y=390.0|img=public/btn_bg_3.png|esc=0>
INC S$行会_Android面板数据 <Img|id=hhlb14|x=510.0|y=390.0|img=private/hanghui/hh_chuangjianhanghui.png|esc=0|link=@行会_Android创建行会页面>
INC S$行会_Android面板数据 <ListView|id=hhlb10|children={<$STR(S$行会_行会列表子项)>}|x=20.0|y=115.0|width=605|height=270|loadDelay=0|margin=4>
GOTO @行会_Android面板
[@行会_申请加入行会]
#IF
HaveGuild
#ACT
sendmsg 19 你已经加入行会，请勿重复申请！
break
#IF
TRUE
#ACT
ReadConfigFileItem ..\QuestDiary\读取数据\行会信息.txt 行会信息 <$GUILDINFO(<$STR(S$行会_申请行会名)>).CHIEF>|<$STR(S$行会_申请行会名)>|是否开启自动收人 N$行会_是否自动接受
#IF
EQUAL <$STR(N$行会_是否自动接受)> 1
#ACT
ChangeGuildMember <$STR(S$行会_申请行会名)> <$USERNAME> 5
sendmsg 6 恭喜你加入<$STR(S$行会_申请行会名)>行会！
break

#IF
PosInlist <$STR(S$行会_申请行会名)>|<$USERNAME> ..\QuestDiary\读取数据\行会申请列表.txt
#ACT
sendmsg 19 你已经申请过该行会了，请勿重复申请！
break
#ELSEACT
ADDVARLIST <$STR(S$行会_申请行会名)>|<$USERNAME>|<$LEVEL> ..\QuestDiary\读取数据\行会申请列表.txt
#IF
TRUE
#ACT
MOV N$行会_页签ID 7
GOTO @行会_Android行会列表页签

[@行会_行会宣战]
#IF
!ISGUILDMASTER
#ACT
MESSAGEBOX 　你不是行会会长\无法对其他行会宣战
BREAK

#IF
!EQUAL $STR(S$行会_宣战行会名) $GUILDNAME
#ACT
QueryMsg 　你确定要花费100万金币\对[<$STR(S$行会_宣战行会名)>]发起行会战吗？ @确定发起行会战
BREAK
#ELSEACT
MESSAGEBOX 不能对自己行会宣战
BREAK
[@确定发起行会战1]
#IF
!CheckGold 1000000
#ACT
MESSAGEBOX 你的金币不足
BREAK
#ELSEACT
MONEY 金币 - 1000000
GuildWar $STR(S$行会_宣战行会名) 120
SENDMSG 19 成功对<$STR(S$行会_宣战行会名)>行会发起行会战，持续2小时
SENDMSG 9 <<$GUILDNAME>>开启了与<$STR(S$行会_宣战行会名)>的行会战 249
BREAK


[@行会_自动同意入会]
#IF
!IsGuildMaster
#ACT
sendmsg 19 你不是会长，无法操作此项！
break

#IF
TRUE
#ACT
ReadConfigFileItem ..\QuestDiary\读取数据\行会信息.txt 行会信息 <$GUILD.CHIEF>|<$GUILD.NAME>|是否开启自动收人 N$行会_自动收人

#IF
EQUAL <$STR(N$行会_自动收人)> 0
#ACT
CALCVAR GUILD 行会_自动同意入会 = 1 1
WriteConfigFileItem ..\QuestDiary\读取数据\行会信息.txt 行会信息 <$GUILD.CHIEF>|<$GUILD.NAME>|是否开启自动收人 1
#ELSEACT
CALCVAR GUILD 行会_自动同意入会 = 0 1
WriteConfigFileItem ..\QuestDiary\读取数据\行会信息.txt 行会信息 <$GUILD.CHIEF>|<$GUILD.NAME>|是否开启自动收人 0
[@行会_Android行会招募页签]
#IF
!IsGuildMaster
#ACT
sendmsg 19 你不是会长，无法操作此项!
break
#IF
!EQUAL N$行会_页签ID 1
#ACT
MOV N$行会_页签ID 1
MOV N$行会_循环计次 0
MOV S$行会_Android面板数据  
MOV S$行会_行会列表子项 
MOV N$行会_一键通过循环计次 0
#IF
TRUE
#ACT
ReadRandomStr ..\QuestDiary\读取数据\行会申请列表.txt S$行会_申请人信息 <$STR(N$行会_循环计次)>
#IF
POS <$STR(S$行会_申请人信息)> <$GUILD.NAME>
#ACT
split <$STR(S$行会_申请人信息)> | S$行会_申请人信息分割
#IF
POS <$STR(S$行会_申请人信息)> <$GUILD.NAME>
CheckOnLine <$STR(S$行会_申请人信息分割_1)>
#ACT
MOV N$行会_申请人状态颜色 218
MOV S$行会_申请人状态 在线
#ELSEACT
MOV N$行会_申请人状态颜色 71
MOV S$行会_申请人状态 离线
#IF
POS <$STR(S$行会_申请人信息)> <$GUILD.NAME>
#ACT
INC S$行会_Android面板数据 <Img|id=hhzm{$STR(N$行会_循环计次)}|children={hhzm<$STR(N$行会_循环计次)>_0,hhzm<$STR(N$行会_循环计次)>_1,hhzm<$STR(N$行会_循环计次)>_2,hhzm<$STR(N$行会_循环计次)>_3,hhzm<$STR(N$行会_循环计次)>_4,hhzm<$STR(N$行会_循环计次)}_5,hhzm<$STR(N$行会_循环计次)>_6}|x=273.0|y=233.0|img=private/hanghui/hh_liebiaobj.png|esc=0>
INC S$行会_Android面板数据 <Button|id=hhzm{$STR(N$行会_循环计次)}_0|x=0.0|y=0.0|width=160|height=36|color=255|size=18|nimg=|text={$STR(S$行会_申请人信息分割_1)}>
INC S$行会_Android面板数据 <Button|id=hhzm{$STR(N$行会_循环计次)}_1|x=160.0|y=0.0|width=90|height=36|color=255|size=18|nimg=|text={$STR(S$行会_申请人信息分割_2)}>
INC S$行会_Android面板数据 <Button|id=hhzm{$STR(N$行会_循环计次)}_2|x=248.0|y=0.0|width=160|height=36|color={$str(N$行会_申请人状态颜色)}|size=18|nimg=|text={$STR(S$行会_申请人状态)}>
INC S$行会_Android面板数据 <Img|id=hhzm{$STR(N$行会_循环计次)}_3|x=415.0|y=2.0|img=public/btn_bg_4.png|esc=0|link=@行会_通过加入申请&行会_行会申请人={$STR(S$行会_申请人信息分割_1)}>
INC S$行会_Android面板数据 <Img|id=hhzm{$STR(N$行会_循环计次)}_4|x=510.0|y=2.0|img=public/btn_bg_4.png|esc=0|link=@行会_拒绝加入申请&行会_行会申请人={$STR(S$行会_申请人信息分割_1)}>
INC S$行会_Android面板数据 <Img|id=hhzm{$STR(N$行会_循环计次)}_5|x=415.0|y=2.0|img=private/hanghui/hh_tongguo.png|esc=0>
INC S$行会_Android面板数据 <Img|id=hhzm{$STR(N$行会_循环计次)}_6|x=510.0|y=2.0|img=private/hanghui/hh_jujue.png|esc=0>
INC S$行会_行会列表子项 hhzm<$STR(N$行会_循环计次)>,
#IF
SMALL <$str(N$行会_循环计次)> <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)>
#ACT
INC N$行会_循环计次 1
GOTO @行会_Android行会招募页签
#IF
TRUE
#ACT
INC S$行会_Android面板数据 <ListView|id=hhlb10|children={<$STR(S$行会_行会列表子项)>}|x=20.0|y=115.0|loadDelay=1|margin=4|width=605|height=270|rotate=0>
INC S$行会_Android面板数据 <Img|id=hhlb11|x=30.0|y=390.0|img=public/btn_bg_3.png|esc=0|link=@行会_Android一键招募页面>
INC S$行会_Android面板数据 <CheckBox|id=hhlb15|x=240.0|y=392.0|nimg=public/1900000550.png|checkboxid=1|pimg=public/1900000551.png|default={$GUILD(行会_自动同意入会)}|link=@行会_自动同意入会>
INC S$行会_Android面板数据 <Img|id=hhlb12|x=510.0|y=390.0|img=public/btn_bg_3.png|esc=0|link=@行会_Android一键通过>
INC S$行会_Android面板数据 <Img|id=hhlb13|x=30.0|y=390.0|img=private/hanghui/hh_yijianzhaomu.png|esc=0>
INC S$行会_Android面板数据 <Img|id=hhlb14|x=510.0|y=390.0|img=private/hanghui/hh_yijiantongguo.png|esc=0>
MOV S$行会_页签按钮 2|1|2|2
GOTO @行会_Android面板
[@行会_Android一键通过]
#IF
EQUAL <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)> 0
#ACT
sendmsg 19 暂无申请人
#IF
PosInlist <$GUILD.NAME> ..\QuestDiary\读取数据\行会申请列表.txt S$行会_申请人信息
#ACT
split <$STR(S$行会_申请人信息)> | S$行会_申请人
ChangeGuildMember <$GUILD.NAME> <$STR(S$行会_申请人_1)> 5
MOV N$行会_清理列表计次 0
MOV S$行会_行会申请人 <$STR(S$行会_申请人_1)>
LOOPGOTO @行会_清理行会申请列表 <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)>
#IF
SMALL <$STR(N$行会_一键通过循环计次)> <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)>
#ACT
INC N$行会_一键通过循环计次 1
GOTO @行会_Android一键通过
#ELSEACT
MOV N$行会_页签ID -1
GOTO @行会_Android行会招募页签
[@行会_拒绝加入申请]
#IF
PosInlist <$GUILD.NAME>|<$STR(S$行会_行会申请人)> ..\QuestDiary\读取数据\行会申请列表.txt S$行会_申请人信息
#ACT
MOV N$行会_清理列表计次 0
LOOPGOTO @行会_清理行会申请列表 <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)>
MOV N$行会_页签ID 0
GOTO @行会_Android行会招募页签
[@行会_通过加入申请]
#IF
PosInlist <$GUILD.NAME>|<$STR(S$行会_行会申请人)> ..\QuestDiary\读取数据\行会申请列表.txt S$行会_申请人信息
#ACT
ChangeGuildMember <$GUILD.NAME> <$STR(S$行会_行会申请人)> 5
MOV N$行会_清理列表计次 0
LOOPGOTO @行会_清理行会申请列表 <$TXTLINE(..\QuestDiary\读取数据\行会申请列表.txt)>
MOV N$行会_页签ID -1
GOTO @行会_Android行会招募页签
[@行会_清理行会申请列表]
#IF
PosInlist <$STR(S$行会_行会申请人)> ..\QuestDiary\读取数据\行会申请列表.txt S$行会_申请人信息
#ACT
DELVARLIST <$STR(S$行会_申请人信息)> ..\QuestDiary\读取数据\行会申请列表.txt
[@行会_Android创建行会页面]
#IF
HaveGuild
#ACT
sendmsg 19 如需创建行会，请脱离现有行会！
break
#IF
CHECKMONEY 2 < 1000
#ACT
MOV N$行会_创建行会元宝颜色 58
#ELSEACT
MOV N$行会_创建行会元宝颜色 218
#IF
CheckItem 战斗号角 1
#ACT
MOV N$行会_创建行会号角颜色 218
#ELSEACT
MOV N$行会_创建行会号角颜色 58
#IF
TRUE
#ACT
MOV S$行会_Android二级页面 <Img|x=445.0|y=107.0|img=private/hanghui/hh_bj4.png|esc=0|link=0>
INC S$行会_Android二级页面 <Img|x=511.0|y=257.0|img=public/1900000651_3.png>
INC S$行会_Android二级页面 <Img|x=622.0|y=257.0|img=public/1900000651_3.png>
INC S$行会_Android二级页面 <ItemShow|x=511.0|y=257.0|width=70|height=70|itemid=26930|itemcount=1|name=元宝|bgtype=1|showtips=1>
INC S$行会_Android二级页面 <ItemShow|x=622.0|y=257.0|width=70|height=70|itemid=199|itemcount=1|name=战斗号角|bgtype=1|showtips=1>
INC S$行会_Android二级页面 <Button|x=490.0|y=326.0|width=110|height=30|color={$STR(N$行会_创建行会元宝颜色)}|size=18|text=1000>
INC S$行会_Android二级页面 <Button|x=603.0|y=327.0|width=110|height=30|color={$STR(N$行会_创建行会号角颜色)}|size=18|text=<$ITEMCOUNT[战斗号角]>/1>
INC S$行会_Android二级页面 <Img|x=551.0|y=485.0|img=public/btn_bg_3.png|esc=0>
INC S$行会_Android二级页面 <Img|x=634.0|y=381.0|img=public/btn_bg_4.png|esc=0>
INC S$行会_Android二级页面 <Img|x=633.0|y=381.0|img=private/hanghui/hh_ksgm.png|esc=0|link=@行会_快速购买>
INC S$行会_Android二级页面 <Img|x=551.0|y=485.0|img=private/hanghui/hh_queding.png|esc=0|link=@行会_创建行会>
INC S$行会_Android二级页面 <Button|x=710.0|y=101.0|pimg=public/button/btn_close_01.png|nimg=public/button/btn_close_01.png|color=255|size=18|link=@行会_关闭创建行会页面>
INC S$行会_Android二级页面 <Input|x=504.0|y=169.0|width=195|height=35|inputid=1|type=0|place=|mincount=10|color=67|maxcount=7|size=18|placecolor=255>
INC S$行会_Android二级页面 <Text|x=514.0|y=429.0|color=67|size=18|text=请及时在行会面板勾选>
INC S$行会_Android二级页面 <Text|x=533.0|y=455.0|color=67|size=18|text=【自动同意入会】>
GOTO @行会_Android面板
[@行会_快速购买]
#IF
TRUE
#ACT
FUNC 1 @商城
[@行会_创建行会]
#IF
!EQUAL <$NPCINPUT(1)>
#ACT
MOV s$行会_行会起名 <$NPCINPUT(1)>
#ELSEACT
sendmsg 19 请输入行会名！
break
#IFONE
CHECKMONEY 2 < 1000
!CheckItem 战斗号角 1
#ACT
sendmsg 19 材料不足，无法创建行会！
break
#IF
SMALL <$STR(N$行会_VIP等级限制)> 5
#ACT
sendmsg 19 创建行会需要VIP等级不小于5级！
break

#IF
large <$GUILDINFO(<$STR(s$行会_行会起名)>).MEMBERCOUNT> 0
#ACT
sendmsg 19 行会名已存在，请更换行会名！
break
#IF
TRUE
#ACT
TAKE 战斗号角 1
Money 2 - 1000
GmExeCute 建立行会 <$STR(s$行会_行会起名)> <$USERNAME>
CALCVAR GUILD 行会_每日招募次数 = 5 1
CALCVAR HUMAN 行会_贡献值 = 0 1
ADDVARLIST <$USERNAME>|<$STR(s$行会_行会起名)>|是否开启自动收人=0 ..\QuestDiary\读取数据\行会信息.txt
GOTO @行会_关闭创建行会页面
MOV N$行会_页签ID -1
GOTO @行会_Android行会列表页签

[@行会_Android一键招募页面]
#IF
TRUE
#ACT
MOV S$行会_Android二级页面 <Img|x=446.0|y=146.0|img=private/hanghui/hh_bj5.png|esc=0>
INC S$行会_Android二级页面 <ItemShow|x=491.0|y=219.0|width=70|height=70|itemid=26930|itemcount=1|name=元宝|bgtype=1|showtips=1>
INC S$行会_Android二级页面 <Text|x=550.0|y=240.0|color=67|size=18|text=500>
INC S$行会_Android二级页面 <Text|x=630.0|y=240.0|color=67|size=18|text=次数{$GUILD(行会_每日招募次数)}/5>
INC S$行会_Android二级页面 <Img|x=621.0|y=274.0|img=public/btn_bg_4.png|esc=0>
INC S$行会_Android二级页面 <Img|x=551.0|y=395.0|img=public/btn_bg_3.png|esc=0>
INC S$行会_Android二级页面 <Text|x=489.0|y=331.0|color=67|size=18|text=将会向所有在线玩家发送招募>
INC S$行会_Android二级页面 <Text|x=533.0|y=358.0|color=67|size=18|text=邮件，并公屏喊话。>
INC S$行会_Android二级页面 <Img|x=621.0|y=275.0|img=private/hanghui/hh_kscz.png|esc=0|link=@行会拉起充值>
INC S$行会_Android二级页面 <Img|x=551.0|y=395.0|img=private/hanghui/hh_fasong.png|esc=0|link=@行会_发送招募信息>
INC S$行会_Android二级页面 <Button|x=710.0|y=139.0|color=255|size=18|nimg=public/button/btn_close_01.png|link=@行会_关闭创建行会页面>
GOTO @行会_Android面板

[@行会拉起充值]
#IF
TRUE
#ACT
FUNC 1 @前往充值
break

[@行会_发送招募信息]
#IF
small <$GUILD(行会_每日招募次数)> 1
#ACT
sendmsg 19 今日发送招募次数已达上限！请明天再试！
break
#IF
LARGE $MONEY(2) 499
#ACT
money 2 - 500
CALCVAR GUILD 行会_每日招募次数 - 1 1
sendmsg 9 全服公告：<$GUILD.NAME>招人啦！诚邀各路英豪加入！ 215
GMEXECUTE 在线提问 @全服发送邮件&行会_发送邮件行会名=<$GUILD.NAME>
GOTO @行会_Android一键招募页面
#ELSEACT
sendmsg 19 元宝不足，请及时充值元宝！

[@行会_关闭创建行会页面]
#IF
TRUE
#ACT
MOV S$行会_Android二级页面  
GOTO @行会_Android面板
[@行会_Android行会玩法页签]
#IF
!HaveGuild
#ACT
sendmsg 19 你未加入行会，无法操作此项!
break
#IF
!EQUAL N$行会_页签ID 2
#ACT
MOV N$行会_页签ID 2
MOV S$行会_Android仓库兑换提交 
MOV S$行会_Android面板数据  
INC S$行会_Android面板数据 <Img|x=320.0|y=176.0|img=private/hanghui/hh_cangku.png|esc=0>
INC S$行会_Android面板数据 <Layout|x=697.0|y=428.0|width=150|height=30|color=|link=@行会_Android行会仓库界面>
#IF
TRUE
#ACT
INC S$行会_Android面板数据 
#IF
TRUE
#ACT   
MOV S$行会_页签按钮 2|2|1|2
GOTO @行会_Android面板
[@行会_Android行会仓库界面]
#IF
EQUAL <$GUILD(行会_行会仓库)>  
#ACT
MOV S$行会_仓库物品数组 
MOV N$行会_循环计次 0
FOR N$行会_循环计次 = 78
INC S$行会_仓库物品数组 0|0,
INC N$行会_循环计次 1
ENDFOR
#ELSEACT
MOV S$行会_仓库物品数组 <$GUILD(行会_行会仓库)>
split <$STR(S$行会_仓库物品数组)> , S$行会_仓库物品名 N$行会_仓库物品NUM
MOV N$行会_循环计次 0
FOR N$行会_循环计次 = <$DEC.78.N$行会_仓库物品NUM>
INC S$行会_仓库物品数组 0|0,
INC N$行会_循环计次 1
ENDFOR
#IF
TRUE
#ACT
split <$STR(S$行会_仓库物品数组)> , S$行会_仓库物品名 N$行会_仓库物品NUM
MOV S$行会_Android仓库页面 
MOV N$行会_循环计次 0
MOV S$行会_仓库children  
FOR N$行会_循环计次 = 13
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.0>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_0|x=10.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_0|x=10.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.1>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_1|x=92.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_1|x=92.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.2>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_2|x=177.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_2|x=177.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.3>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_3|x=262.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_3|x=262.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.4>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_4|x=347.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_4|x=347.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
split <$STR(S$行会_仓库物品名_<$INC.<$MULT.N$行会_循环计次.6>.5>)> | S$行会_仓库物品名分解
INC S$行会_Android仓库页面 <Img|id=ckgz{$STR(N$行会_循环计次)}_5|x=432.0|y=5.0|img=public/fjfqk.png>
INC S$行会_Android仓库页面 <ItemShow|id=ckitem{$STR(N$行会_循环计次)}_5|x=432.0|y=5.0|width=70|height=70|itemid=0|itemcount={$STR(S$行会_仓库物品名分解_1)}|bgtype=1|showtips=0|name={$STR(S$行会_仓库物品名分解_0)}|link=@行会_仓库物品取出&行会_仓库取出装备名={$STR(S$行会_仓库物品名分解_0)}>
INC S$行会_Android仓库页面 <Layout|id=ckgz{$STR(N$行会_循环计次)}|children={ckgz<$STR(N$行会_循环计次)>_0,ckgz<$STR(N$行会_循环计次)>_1,ckgz<$STR(N$行会_循环计次)>_2,ckgz<$STR(N$行会_循环计次)>_3,ckgz<$STR(N$行会_循环计次)>_4,ckgz<$STR(N$行会_循环计次)>_5,ckitem{$STR(N$行会_循环计次)}_0,ckitem{$STR(N$行会_循环计次)}_1,ckitem{$STR(N$行会_循环计次)}_2,ckitem{$STR(N$行会_循环计次)}_3,ckitem{$STR(N$行会_循环计次)}_4,ckitem{$STR(N$行会_循环计次)}_5}|x=0|y=0|width=500|height=80>
INC S$行会_仓库children ckgz<$STR(N$行会_循环计次)>,
INC N$行会_循环计次 1
ENDFOR
#IF
TRUE
#ACT
MOV S$行会_背包物品数组   
LoopBagItems @行会_背包物品遍历 N$行会_背包物品ID
#IF
!EQUAL <$STR(S$行会_背包物品数组)>
#ACT
SPLIT <$STR(S$行会_背包物品数组)> , S$行会_背包物品ID N$行会_背包物品NUM
MOV N$行会_循环计次 0
FOR N$行会_循环计次 = <$DEC.4.<$mod.N$行会_背包物品NUM.4>>
INC S$行会_背包物品数组 0,
INC N$行会_循环计次 1
endfor
#IF
!EQUAL <$STR(S$行会_背包物品数组)>  
#ACT
SPLIT <$STR(S$行会_背包物品数组)> , S$行会_背包物品ID N$行会_背包物品NUM
MOV N$行会_循环计次 0
MOV S$行会_包裹children  
FOR N$行会_循环计次 = <$DIV.N$行会_背包物品NUM.4>
INC S$行会_Android仓库页面 <Img|id=bggz{$STR(N$行会_循环计次)}_0|x=0|y=5.0|img=public/clk.png>
INC S$行会_Android仓库页面 <ItemShow|id=bbitem{$STR(N$行会_循环计次)}_0|x=0.0|y=5.0|width=70|height=70|itemid=0|itemcount=1|bgtype=1|showtips=1|name={$ITEMINFO[<$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.0>)>].STDNAME}|link=@行会_背包物品被选中&行会_背包装备唯一ID={$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.0>)}>
INC S$行会_Android仓库页面 <Img|id=bggz{$STR(N$行会_循环计次)}_1|x=70.0|y=5.0|img=public/clk.png>
INC S$行会_Android仓库页面 <ItemShow|id=bbitem{$STR(N$行会_循环计次)}_1|x=70.0|y=5.0|width=70|height=70|itemid=0|itemcount=1|bgtype=1|showtips=1|name={$ITEMINFO[<$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.1>)>].STDNAME}|link=@行会_背包物品被选中&行会_背包装备唯一ID={$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.1>)}>
INC S$行会_Android仓库页面 <Img|id=bggz{$STR(N$行会_循环计次)}_2|x=140.0|y=5.0|img=public/clk.png>
INC S$行会_Android仓库页面 <ItemShow|id=bbitem{$STR(N$行会_循环计次)}_2|x=140.0|y=5.0|width=70|height=70|itemid=0|itemcount=1|bgtype=1|showtips=1|name={$ITEMINFO[<$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.2>)>].STDNAME}|link=@行会_背包物品被选中&行会_背包装备唯一ID={$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.2>)}>
INC S$行会_Android仓库页面 <Img|id=bggz{$STR(N$行会_循环计次)}_3|x=210.0|y=5.0|img=public/clk.png>
INC S$行会_Android仓库页面 <ItemShow|id=bbitem{$STR(N$行会_循环计次)}_3|x=210.0|y=5.0|width=70|height=70|itemid=0|itemcount=1|bgtype=1|showtips=1|name={$ITEMINFO[<$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.3>)>].STDNAME}|link=@行会_背包物品被选中&行会_背包装备唯一ID={$STR(S$行会_背包物品ID_<$INC.<$MULT.N$行会_循环计次.4>.3>)}>
INC S$行会_Android仓库页面 <Layout|id=bggz{$STR(N$行会_循环计次)}|children={bggz<$STR(N$行会_循环计次)>_0,bggz<$STR(N$行会_循环计次)>_1,bggz<$STR(N$行会_循环计次)>_2,bggz<$STR(N$行会_循环计次)>_3,bbitem<$STR(N$行会_循环计次)>_0,bbitem<$STR(N$行会_循环计次)>_1,bbitem<$STR(N$行会_循环计次)>_2,bbitem<$STR(N$行会_循环计次)>_3}|x=0|y=0|width=500|height=80>
INC S$行会_包裹children bggz<$STR(N$行会_循环计次)>,
INC N$行会_循环计次 1
ENDFOR
#IF
TRUE
#ACT
INC S$行会_Android仓库页面 <Img|x=186.0|y=58.0|img=private/hanghui/hh_hhckdb.png|esc=0|bj=1|link=0>
INC S$行会_Android仓库页面 <Img|x=165.0|y=14.0|img=public/frame_squared_outside.png|esc=0|link=0>
INC S$行会_Android仓库页面 <Text|x=421.0|y=111.0|color=67|size=20|text=行会仓库>
INC S$行会_Android仓库页面 <Text|x=591.0|y=20.0|color=67|size=22|text=仓库>
INC S$行会_Android仓库页面 <Text|x=851.0|y=111.0|color=67|size=20|text=背包>
INC S$行会_Android仓库页面 <ListView|children={<$STR(S$行会_仓库children)>}|x=210.0|y=162.0|width=500|height=330|img=picpackage/3/9998.png>
INC S$行会_Android仓库页面 <ListView|children={<$STR(S$行会_包裹children)>}|x=735.0|y=150.0|width=280|height=310|img=picpackage/3/9998.png>
INC S$行会_Android仓库页面 <Button|x=730.0|y=468.0|nimg=|color=67|size=18|width=280|height=30|text=当前贡献:{$HUMAN(行会_贡献值)}>
INC S$行会_Android仓库页面 <Button|x=974.0|y=15.0|nimg=public/button/btn_close_01.png|pimg=public/button/btn_close_01.png|link=@行会_关闭仓库>
INC S$行会_Android仓库页面 <$STR(S$行会_Android仓库兑换提交)>
INC S$行会_Android仓库页面 <$STR(S$行会_Android仓库取出提交)>
GOTO @行会_Android面板
MOV N$行会_循环计次 0
[@行会_仓库物品取出]
#IF
EQUAL <$STR(S$行会_仓库取出装备名)> 0
#ACT
BREAK
#IF
POS <$GUILD(行会_行会仓库)> <$STR(S$行会_仓库取出装备名)> 
PosInlist <$STR(S$行会_仓库取出装备名)> ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_占位变量 N$行会_物品数据行号
#ACT
MOV S$行会_Android仓库兑换提交 <Img|x=459.0|y=178.0|width=220|height=300|img=private/npcbj/bg_energy_01.png|bg=1|esc=0|link=0>
INC S$行会_Android仓库兑换提交 <Text|x=534.0|y=182.0|color=67|size=18|text=取出物品>
INC S$行会_Android仓库兑换提交 <Img|x=537.0|y=224.0|width=70|height=68|img=public/fjfqk.png|esc=0>
INC S$行会_Android仓库兑换提交 <ItemShow|x=537|y=224|width=70|height=70|itemid=1|itemcount=1|name={$STR(S$行会_仓库取出装备名)}|bgtype=1|showtips=1>
INC S$行会_Android仓库兑换提交 <Button|x=459.0|y=299.0|width=220|height=30|color=255|size=18|nimg=|text={$STR(S$行会_仓库取出装备名)}>
INC S$行会_Android仓库兑换提交 <Button|x=483.0|y=420.0|width=60|color=67|size=18|nimg=public/btn_bg_4.png|text=取出|link=@行会_仓库物品取出到背包>
INC S$行会_Android仓库兑换提交 <Button|x=588.0|y=420.0|width=60|color=67|size=18|nimg=public/btn_bg_4.png|text=关闭|link=@行会_背包物品存入取消>
ReadRandomStr ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_物品数据提取 <$STR(N$行会_物品数据行号)>
split <$STR(S$行会_物品数据提取)> = S$行会_物品数据分割
#IF
small <$HUMAN(行会_贡献值)> <$STR(S$行会_物品数据分割_1)>
#ACT
MOV N$行会_取出金额颜色 58
#ELSEACT
MOV N$行会_取出金额颜色 218
#IF
TRUE
#ACT
INC S$行会_Android仓库兑换提交 <Button|x=460.0|y=371.0|width=220|height=30|color={$STR(N$行会_取出金额颜色)}|nimg=|size=18|text={$STR(S$行会_物品数据分割_1)}点贡献值>
INC S$行会_Android仓库兑换提交 <Button|x=460.0|y=340.0|width=220|height=30|nimg=|color=218|size=18|text=取出该物品需要>
GOTO @行会_Android行会仓库界面
[@行会_仓库物品取出到背包]
#IF
!POS <$GUILD(行会_行会仓库)> <$STR(S$行会_仓库取出装备名)>
#ACT
MOV S$行会_Android仓库兑换提交 
GOTO @行会_Android行会仓库界面
sendmsg 19 手慢啦，物品已被取出！
break
#IF
small <$HUMAN(行会_贡献值)> <$STR(S$行会_物品数据分割_1)>
#ACT
MOV S$行会_Android仓库兑换提交 
GOTO @行会_Android行会仓库界面
sendmsg 19 贡献点不足！
break
#IF
CheckBagSize 1
#ACT
#ELSEACT
sendmsg 19 背包已满，请清理背包后再试！
#IF
TRUE
#ACT
split <$GUILD(行会_行会仓库)> , S$行会_仓库取出物品数据
#IF
!POS <$STR(S$行会_仓库取出物品数据_<$STR(N$行会_循环计次)>)> <$STR(S$行会_仓库取出装备名)>
#ACT
INC N$行会_循环计次 1
GOTO @行会_仓库物品取出到背包
#ELSEACT
CALCVAR HUMAN 行会_贡献值 - <$STR(S$行会_物品数据分割_1)> 1
MOV S$行会_仓库物品原数据 <$GUILD(行会_行会仓库)>
split <$STR(S$行会_仓库取出物品数据_<$STR(N$行会_循环计次)>)> | S$行会_仓库取出物品
ReplaceText S$行会_仓库物品原数据 <$STR(S$行会_仓库取出物品数据_<$STR(N$行会_循环计次)>)> <$STR(S$行会_仓库取出装备名)>|<$DEC.<$STR(S$行会_仓库取出物品_1)>.1>
ReplaceText S$行会_仓库物品原数据 <$STR(S$行会_仓库取出装备名)>|0,  
CALCVAR GUILD 行会_行会仓库 = $STR(S$行会_仓库物品原数据)> 1
GIVE <$STR(S$行会_仓库取出装备名)> 1
MOV S$行会_Android仓库兑换提交 
GOTO @行会_Android行会仓库界面
[@行会_背包物品遍历]
#IF
PosInlist <$ITEMINFO[<$STR(N$行会_背包物品ID)>].STDNAME> ..\QuestDiary\读取数据\行会仓库贡献值.txt
#ACT
INC S$行会_背包物品数组 <$STR(N$行会_背包物品ID)>,
[@行会_背包物品被选中]
#IF
!EQUAL <$STR(S$行会_背包装备唯一ID)> 0
PosInlist <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME> ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_占位变量 N$行会_物品数据行号
#ACT
MOV N$行会_循环计次 0
MOV S$行会_Android仓库兑换提交 <Img|x=459.0|y=178.0|width=220|height=300|img=private/npcbj/bg_energy_01.png|bg=1|esc=0|link=0>
INC S$行会_Android仓库兑换提交 <Text|x=534.0|y=182.0|color=67|size=18|text=贡献物品>
INC S$行会_Android仓库兑换提交 <Img|x=537.0|y=224.0|width=70|height=68|img=public/fjfqk.png|esc=0>
INC S$行会_Android仓库兑换提交 <ItemShow|x=537|y=224|width=70|height=70|itemid=1|itemcount=1|name={$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME}|bgtype=1|showtips=1>
INC S$行会_Android仓库兑换提交 <Button|x=459.0|y=299.0|width=220|height=30|color=255|size=18|nimg=|text={$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME}>
INC S$行会_Android仓库兑换提交 <Button|x=483.0|y=420.0|width=60|color=67|size=18|nimg=public/btn_bg_4.png|text=提交|link=@行会_背包物品存入仓库>
INC S$行会_Android仓库兑换提交 <Button|x=588.0|y=420.0|width=60|color=67|size=18|nimg=public/btn_bg_4.png|text=关闭|link=@行会_背包物品存入取消>
ReadRandomStr ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_物品数据提取 <$STR(N$行会_物品数据行号)>
split <$STR(S$行会_物品数据提取)> = S$行会_物品数据分割
INC S$行会_Android仓库兑换提交 <Button|x=460.0|y=371.0|width=220|height=30|color=149|nimg=|size=18|text={$STR(S$行会_物品数据分割_1)}点贡献值>
INC S$行会_Android仓库兑换提交 <Text|x=497.0|y=340.0|color=218|size=18|text=贡献该物品将获得>
GOTO @行会_Android行会仓库界面
[@行会_背包物品存入取消]
#IF
TRUE
#ACT
MOV S$行会_Android仓库兑换提交 
GOTO @行会_Android行会仓库界面
[@行会_背包物品存入仓库]
#IF
!EQUAL <$GUILD(行会_行会仓库)> 
#ACT
split <$GUILD(行会_行会仓库)> , S$行会_仓库物品名 N$行会_仓库物品NUM
#ELSEACT
MOV N$行会_仓库物品NUM 0
#IF
!small <$STR(N$行会_仓库物品NUM)> 78
#ACT
sendmsg 19 行会仓库已满，请联系会长及时清理行会仓库！
break
#IF
!pos <$GUILD(行会_行会仓库)> <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME>
CheckItemID <$STR(S$行会_背包装备唯一ID)>
PosInlist <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME> ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_占位变量 N$行会_物品数据行号
#ACT
MOV S$行会_要存入装备名 <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME>
TAKEM <$STR(S$行会_背包装备唯一ID)>
CALCVAR GUILD 行会_行会仓库 + <$STR(S$行会_要存入装备名)>|1, 1
ReadRandomStr ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_物品数据提取 <$STR(N$行会_物品数据行号)>
split <$STR(S$行会_物品数据提取)> = S$行会_物品数据分割
CALCVAR HUMAN 行会_贡献值 + <$STR(S$行会_物品数据分割_1)> 1
MOV S$行会_Android仓库兑换提交  
GOTO @行会_Android行会仓库界面
break
#IF
pos <$GUILD(行会_行会仓库)> <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME>
!pos <$STR(S$行会_仓库物品名_<$STR(N$行会_循环计次)>)> <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME>
CheckItemID <$STR(S$行会_背包装备唯一ID)>
PosInlist <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME> ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_占位变量 N$行会_物品数据行号
#ACT
INC N$行会_循环计次 1
GOTO @行会_背包物品存入仓库
#ELSEACT
MOV S$行会_要存入装备名 <$ITEMINFO[<$STR(S$行会_背包装备唯一ID)>].STDNAME>
TAKEM <$STR(S$行会_背包装备唯一ID)>
split <$STR(S$行会_仓库物品名_<$STR(N$行会_循环计次)>)> | S$行会_拆分仓库物品
MOV S$行会_仓库物品原数据 <$GUILD(行会_行会仓库)>
ReplaceText S$行会_仓库物品原数据 <$STR(S$行会_仓库物品名_<$STR(N$行会_循环计次)>)> <$STR(S$行会_要存入装备名)>|<$INC.<$STR(S$行会_拆分仓库物品_1)>.1> 1
CALCVAR GUILD 行会_行会仓库 = <$STR(S$行会_仓库物品原数据)> 1
ReadRandomStr ..\QuestDiary\读取数据\行会仓库贡献值.txt S$行会_物品数据提取 <$STR(N$行会_物品数据行号)>
split <$STR(S$行会_物品数据提取)> = S$行会_物品数据分割
CALCVAR HUMAN 行会_贡献值 + <$STR(S$行会_物品数据分割_1)> 1
#IF
large <$HUMAN(行会_贡献值)> 40000000
#ACT
CALCVAR HUMAN 行会_贡献值 = 40000000 1
#IF
TRUE
#ACT
MOV S$行会_Android仓库兑换提交  
GOTO @行会_Android行会仓库界面
[@行会_关闭仓库]
#IF
TRUE
#ACT
MOV S$行会_Android仓库页面

GOTO @行会_Android面板
[@行会_Android我的行会页签]
#IF
!HaveGuild
#ACT
sendmsg 19 你未加入公会，无法打开面板
break

#IF
TRUE
#ACT
OPENUI 1202 1
close

}

















